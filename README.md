# PN532 NFC Library

This library allows for integration of NXP's [PN532/C106 NFC Module](http://www.nxp.com/documents/short_data_sheet/PN532_SDS.pdf) in an Electric Imp project.  This device supports a wide range of NFC technologies, including read, write, and emulation support for MIFARE, FeliCa, and ISO/IEC 14443A tags.  It also supports peer-to-peer communication and has power-saving options.

This library currently contains two classes:

- The [`PN532`](#PN532) class contains the base code needed to initialize the PN532, communicate with it, and read from generic NFC tags.
- The [`PN532MifareClassic`](#PN532_MiFare_Classic) class contains functions needed to read and write to [MIFARE Classic](http://www.nxp.com/products/identification_and_security/smart_card_ics/mifare_smart_card_ics/mifare_classic/) tags, which support read/write storage.  It also serves as an example of how to build on the `PN532` class to interface with the many other protocols and features that the PN532 supports.

### Examples

(TODO)

# PN532 Class

## Constructor: PN532(*spi, nss, rstpd, irq, callback*)

Creates and initializes an object representing the PN532 NFC device.

- *spi* should be a [SPI object](https://electricimp.com/docs/api/hardware/spi/) pre-configured with the flags `LSB_FIRST | CLOCK_IDLE_HIGH` and a clock rate.  The PN532 supports clock rates up to 5 MHz.
- *nss* should be a pin connected to the PN532's not-selected line.
- *rstpd* should be a pin connected to the PN532's RSTPDN (Reset/Power-Down) pin.
- *irq* should be a pin connected to the PN532's P70_IRQ pin.
- *callback* is a function that will be called when object instantiation is complete.  It takes one *error* parameter that is null upon successful instantiation.

```squirrel
#require "PN532.class.nut:1.0.0"

local spi = hardware.spi257;
local nss = hardware.pin1;
local rstpd = hardware.pin3;
local irq = hardware.pin4;

spi.configure(LSB_FIRST | CLOCK_IDLE_HIGH, 2000);

function constructorCallback(error) {
    if(error != null) {
        server.log("Error constructing PN532: " + error);
        return;
    }
    // It's now safe to use the PN532
}

reader <- PN532(spi, nss, rstpd, irq, constructorCallback);
```

## init(*callback*)

Configures the PN532 with settings for later use.  The *callback* is called upon completion and takes one *error* parameter that is null on success.

This method must be called whenever the PN532 is power-cycled, but is automatically called by the constructor.

```squirrel
function initCallback(error) {
    if(error != null) {
        server.log("Error re-initializing PN532: " + error);
        return;
    }
    // It's now safe to use the PN532
}

// Power cycle the device
rstpd.write(0);
rstpd.write(1);

// Reinitialize
reader.init(initCallback);
```

## getFirmwareVersion(*callback*)

Queries the PN532 for its internal firmware version number.

Takes a callback that takes two parameters:

- *error*: A string that is null on success.
- *version*: A table that contains fields for *IC*, *Ver*, *Rev*, and *Support* as documented in the `GetFirmwareVersion` command from the [PN532 datasheet](http://www.nxp.com/documents/user_manual/141520.pdf).

```squirrel
function firmwareVersionCallback(error, version) {
        server.log(format("Firmware version: %X:%X.%X-%X", version, version.ver, version.rev, version.support));
}

reader.getFirmwareVersion(firmwareVersionCallback);
```

## getNearbyTags(*tagType, initiatorData, callback*)

Searches for nearby NFC tags of type *tagType* and passes scan results to the *callback*.

*tagType* is an integer representing the baud rate and initialization protocol used during the scan.  It must be taken from the following static class members:

- `PN532.TAG_TYPE_106_A`: 106 kbps ISO/IEC14443 Type A
- `PN532.TAG_TYPE_212_FELICIA`: 212 kbps FeliCa polling
- `PN532.TAG_TYPE_424_FELICIA`: 424 kbps FeliCa polling
- `PN532.TAG_TYPE_106_B`: 106 kbps ISO/IEC14443-3B
- `PN532.TAG_TYPE_106_JEWEL`: 106 kbps Innovision Jewel

*initiatorData* is a blob containing the initialization data specific to the selected protocol.  For a detailed description of the format of this argument, see the PN532 datasheet.

*callback* is a function with the following arguments:

- *error*: A string that is null on success.
- *numTagsFounds*: The number of tags found during the scan (currently either 1 or 0)
- *tagData*: A table that is non-null only when *numTagsFound* is 1 and contains the following fields:
 - *SENS_RES*: A 2-byte blob representing the SENS_RES/ATQA field of the tag
 - *SEL_RES*: 1 byte representing the SEL_RES/SAK field of the tag
 - *NFCID*: A blob representing the semi-unique UID of the tag (usually either 4 or 7 bytes)
 - *ATS*: The ATS response, if generated by the tag

```squirrel
function scanCallback(error, numTagsFound, tagData) {
    if(error != null) {
        server.log(error);
        return;
    }
    
    if(numTagsFound > 0) {
        server.log("Found a tag:");
        server.log(format("SENS_RES: %X %X", tagData.SENS_RES[0], tagData.SENS_RES[1]));
        server.log("NFCID:");
        server.log(tagData.NFCID);
    } else {
        server.log("No tags found");
    }
}

reader.getNearbyTags(PN532.TAG_TYPE_106_A, scanCallback);
```

**The following methods are exposed for use in creating extensions of the PN532 class to support extra protocols and commands.**

## PN532.makeDataExchangeFrame(*tagNumber, data*)

Constructs a data exchange frame for use in [PN532.sendRequest()](#sendrequestrequestframe-responsecallback--numretries).

*tagNumber* is the index number of the tag in the current field.  Currently this is always 1.

*data* is the payload blob for this frame.

See the [PN532 datasheet](http://www.nxp.com/documents/user_manual/141520.pdf) for proper use of the data exchange frame.

```squirrel
function makeMifareReadFrame(address) {
    local frame = blob(2);

    frame.writen(0x30, 'b');
    frame.writen(address, 'b');
        
    return PN532.makeDataExchangeFrame(1, frame);
}
```

## sendRequest(*requestFrame, responseCallback [, numRetries]*)

Sends the specified *requestFrame* to the PN532 and associates a *responseCallback* to handle the response.  Optionally allows for a maximum number of retries due to transmission failures (defaults to 5).

*requestFrame* must be a frame generated by [PN532.makeDataExchangeFrame()](# pn532makedataexchangeframetagnumber-data).

*responseCallback* must be a function that takes the following arguments:

- *error*: A string that is null on success.
- *responseData*: A blob containing the raw response from the PN532 to the request.

```squirrel
function responseCallback(error, responseData) {
    if(error != null) {
        imp.wakeup(0, function() {
            userCallback(error, null);
        });
        return;
    }
            
    // Process responseData...
    server.log(responseData.tostring());
}

local frame = makeMifareReadFrame(0x3);
reader.sendRequest(frame, responseCallback);  
```

# PN532 MIFARE Classic Class

## Constructor: PN532MifareClassic(*pn532*)

Associates this class with a previously constructed *pn532* object.

```squirrel
#require "PN532.class.nut:1.0.0"
#require "PN532MifareClassic.class.nut:1.0.0"

// ... setup PN532 pins and callback ...

local reader = PN532(spi, nss, rstpd, irq, constructorCallback);
mifareReader <- PN532MifareClassic(reader);
```

## getNearbyTags(*callback*)

A wrapper around the PN532 class's [getNearbyTags()](#getnearbytagstagtype-initiatordata-callback) method that configures it to search for MIFARE NFC tags.

```squirrel
mifareReader.getNearbyTags(scanCallback);
```

## authenticate(*tagSerial, address, aOrB, key, callback*)

Attempts to authenticate the reader to perform operations on a specified EEPROM address.

*tagSerial*: A blob representing the UID of the tag to be authenticated. This UID can be taken from the response to the [getNearbyTags()](#getnearbytags-callback) call.

*address*: The address of the memory block to be authenticated. For the MIFARE Classic 1k, this is an integer in the range 0-63.

*aOrB*: The key type to authenticate against.  The options are the static class members `PN532MifareClassic.AUTH_TYPE_A` and `PN532MifareClassic.AUTH_TYPE_B`.

*key*: The key to use when authenticating for the given block.  If no key has been set, set this parameter to null to use the default FFFFFFFFFFFFh value.

*callback*: A function taking the following arguments:

- *error*: A string that is null on success.
- *status*: A boolean that is set to true if the authentication succeeded.

```squirrel
function authCallback(error, status) {
    if(error != null) {
        server.log(error);
        return;
    }
    
    if(authenticated) {
        // Operate on tag...
    }
}

mifareReader.authenticate(tagData.NFCID, 0x2, PN532MifareClassic.AUTH_TYPE_A, null, authCallback);
```

## read(*address, callback*)

Reads 16 bytes from the *address* specified on the currently authenticated tag.

*callback*: A function taking the following arguments:

- *error*: A string that is null on success.
- *data*: A 16-byte blob containing the data read from the tag.

Note that this call will fail with an error if the address being read has not previously been authenticated.

```squirrel
// ...authenticate the address...

function readCallback(error, data) {
    if(error != null) {
        server.log(error);
        return;
    }
    server.log(data.tostring());
}

mifareReader.read(0x2, readCallback);
```

## write(*address, data, callback*)

Writes 16 bytes of blob *data* to the *address* specified on the currently authenticated tag.

*callback* is a function taking an *error* string that is null on success.

Note that this call will fail with an error if the address being read has not previously been authenticated or if *data* is not exactly 16 bytes long.  

```squirrel
// ...authenticate the address...

function writeCallback(error) {
    if(error != null) {
        server.log(error);
        return;
    }
}

local data = blob(16);
for(local i = 0; i < 16; i++) {
    data[i] = 88;
}

mifareReader.write(0x2, data, writeCallback);
```

# License

The PN532 library is licensed under the [MIT License](./LICENSE).
